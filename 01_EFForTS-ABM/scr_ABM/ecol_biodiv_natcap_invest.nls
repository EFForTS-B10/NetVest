
to natcap_invest_habitatquality_init
; input is located in model folder ./natcap_invest/input: input for invest is: sensitivitytable.txt, impacttable.txt, LULCmap.asc, impactmaps.asc"
; output will be written to model folder ./natcap_invest/output: output of EFForTS-ABM as inpt for Invest is: sensitivitytable.csv, impacttable.csv, LULCmap.tif, impacttables.tif"
; for testing purposes workdir can be set via full path to point to the testing data files

  set workdir "./natcap_invest"
  natcap_invest_set_up_invest 
  natcap_invest_write_tables 
end

;; Setup the connection to python and import a few libraries
to natcap_invest_set_up_invest
 print["setting up python invest"]
  py:setup py:python3
  py:run "import sys"
  py:run "import os"
  py:run "import logging"
  py:run "import natcap.invest"
  py:run "from natcap.invest import habitat_quality"
  py:run "from osgeo import gdal, osr"
end



;; Generation of csv-tables
to natcap_invest_write_tables
 print ["start write tables"]
  natcap_invest_write_sensitivity 
  natcap_invest_write_impact 
end

;; Read in sensitivity-table from natcap_invest input-folder and generate sensitivity-table
to natcap_invest_write_sensitivity
 print ["writing sensitivity table"]
 print (word "workdir:" workdir)
  ;; Reset global sensitivity-list
  let sensitivity_all []
  let filename  0
  ;; Read sensitivity file depending on habitat quality objective
     set filename word workdir "/input/sensitivitytable.txt"
     print (word "sensitivity filename:" filename)
  ifelse filename = 0 [stop]
  [
  file-open filename
                                                          ;print ["file open"]
  while [not file-at-end?]
  [

    ;;Read data for previous defined research objective and correlated habitat-suitability
    let LULC file-read
                                                           ;print LULC
    let NAME file-read
    let HABITAT file-read
    let oilpalm file-read
    let rubber file-read

    ;; Generate temporary sensitivity-list
    let sensitivity (list LULC NAME HABITAT oilpalm rubber)
    set sensitivity_all lput sensitivity sensitivity_all

 
  ]
    ;; Store sensitivity_table.csv to output folder
    let filename_out (word workdir "/output/sensitivity_table.csv")
    csv:to-file filename_out sensitivity_all

  file-close
  ]

end


;; Read in impact-table from natcap_invest input-folder and generate impact-table
to natcap_invest_write_impact
 print ["writing impact table"]
  let impact_all []
  let filename word workdir "/input/impacttable.txt"
 print (word "impact filename:" filename)
  ifelse filename = 0 [stop]
  [
  file-open filename

  while [not file-at-end?]
  [
    ;;Read data for impact-table
    let THREAT file-read
    let MAX_DIST file-read
    let WEIGHT file-read
    let DECAY file-read
    let CUR_PATH file-read    

    ;; Generate temporary impact-list
    let impact (list THREAT MAX_DIST WEIGHT DECAY CUR_PATH)
    set impact_all lput impact impact_all
  ]
    ;; Store impact_table.csv to output folder
    let filename_out word workdir "/output/impact_table.csv"
    csv:to-file filename_out impact_all

  file-close
  ]
end


to natcap_invest_habitatquality_update
  set workdir "./natcap_invest"  
  let invest_wd word workdir "/output"
  let experiment natcap_invest_experiment 
  let half-saturation-constant k
   natcap_invest_translate_lulc
   natcap_invest_write_maps 
   natcap_invest_convert_maps_tif 
   natcap_invest_execute_invest invest_wd experiment half-saturation-constant
   natcap_invest_convert_maps_asc experiment 
   natcap_invest_save_habitatquality_to_patch experiment 
   natcap_invest_aggregate_habitatquality_landscape
   natcap_invest_aggregate_habitatquality_landuse
end


;; Create new variable for lulc-classification used by natcap InVEST
to natcap_invest_translate_lulc
 print["translating lulc values"]
  ask patches
  [
    set p_landuse_invest abs p_landuse  
  ]
end

;; Generation of LULC-raster-map and impact-maps (as much as impacts we have)
to natcap_invest_write_maps
 print["start write maps to output folder"]
  natcap_invest_write_lulc_map 
  natcap_invest_write_impact_map "oilpalm" 
  natcap_invest_write_impact_map "rubber" 
end


;; Generation of LULC-map
to natcap_invest_write_lulc_map
 print ["writing lulc map"]
  let new-raster gis:patch-dataset p_landuse_invest
  let filename (word workdir "/output/lulc.asc")
  print filename
  if (file-exists? filename) [file-delete filename]
  gis:store-dataset new-raster filename
end

;; Generation of impact-maps
to natcap_invest_write_impact_map [impact-name]
  ask patches
  [set p_impact-location FALSE]
 print["writing impact map(s)"]
  ;; iterate throug all possible impacts
  show impact-name
  (ifelse
    impact-name = "oilpalm"
    [ask patches with [p_landuse_invest = 0] [set p_impact-location TRUE] 
       print ["generating oilpalm impact map"]]
    impact-name = "rubber"
      [ask patches with [p_landuse_invest = 1 ] [set p_impact-location TRUE] 
       print ["generating rubber impact map"]]
  )
  let new-raster gis:create-raster (max-pxcor + 1) (max-pycor + 1) gis:world-envelope
  let xcount 0
  let ycount max-pycor
  let ycount-raster 0
  while [ycount >= 0]
  [
    while[xcount <= max-pxcor]
    [
      ifelse ([p_impact-location] of patch xcount ycount = TRUE)
          [gis:set-raster-value new-raster xcount ycount-raster 1]
          [gis:set-raster-value new-raster xcount ycount-raster 0]
      set xcount xcount + 1
    ]
    set xcount 0
    set ycount ycount - 1
    set ycount-raster ycount-raster + 1
  ]

  ;; store impact-map
  let filename (word workdir "/output/" (word (impact-name)) "_c.asc")
  if (file-exists? filename) [file-delete filename]
  gis:store-dataset new-raster filename

  ask patches [set p_impact-location FALSE]
end


;; Conversion of asc-maps to tif-maps for transferation to InVEST
to natcap_invest_convert_maps_tif
 print ["converting maps"]
 print (word "workdir: "workdir)
  foreach ["lulc" "oilpalm_c" "rubber_c"]
[
    [x] ->
    natcap_invest_convert_to_tif (x)
  ]
end


to natcap_invest_convert_to_tif [file]
let filepath (word workdir "/output/" file)
  py:set "filepath" filepath
 print (word "filepath:" filepath)
  (py:run
    "drv = gdal.GetDriverByName('GTiff')"
    "ds_in = gdal.Open(f'{filepath}.asc')"
    "ds_out = drv.CreateCopy(f'{filepath}.tif', ds_in)"
    "srs = osr.SpatialReference()"
    "srs.ImportFromEPSG(32748)"
    "ds_out.SetProjection(srs.ExportToWkt())"
    "ds_out.GetRasterBand(1).SetNoDataValue(-9999)"
    "ds_in = None"
    "ds_out = None"
  )
end


;; Invoke model InVEST and transfer output
to natcap_invest_execute_invest [invest_wd experiment half-saturation-constant]
 print (word "[invoking model InVEST with working directory: " invest_wd "]")
 print (word "experiment name: " experiment)
 print (word "half saturation constant:" k)
  py:set "working_directory" invest_wd
  py:set "experiment_name" experiment
  py:set "half_saturation_constant" half-saturation-constant
  (py:run
 "print('setting up parameters')"
  "logfile = working_directory + '/invest_log_' + experiment_name + '.log'" 
  "logging.basicConfig(filename=logfile, level=logging.DEBUG)"
	"logging.info('Starting habitat_quality.execute')"
	
    "args = {"
    "'half_saturation_constant': half_saturation_constant,"
    "'results_suffix': experiment_name,"
    "'workspace_dir': working_directory,"
    "'lulc_cur_path': os.path.join(working_directory, 'lulc.tif'),"
    "'sensitivity_table_path': os.path.join(working_directory, 'sensitivity_table.csv'),"
    "'threat_raster_folder': working_directory,"
    "'threats_table_path': os.path.join(working_directory, 'impact_table.csv'),"
    "}"
    "habitat_quality.execute(args)"
  )
end


;; Convertion of tif-file from InVEST to asc-file for netlogo
to natcap_invest_convert_maps_asc [experiment]
  print (word "[converting invest results to asc for " experiment "]")
  py:set "working_directory" workdir
  (py:run
    "filename = f'{working_directory}/output/quality_c_{experiment_name}'"
    "in_name = f'{filename}.tif'"
    "out_name = f'{filename}.asc'"
    "gdal.Translate(out_name, in_name,format='AAIGrid')"
  )
   (py:run
    "filename = f'{working_directory}/output/deg_sum_c_{experiment_name}'"
    "in_name = f'{filename}.tif'"
    "out_name = f'{filename}.asc'"
    "gdal.Translate(out_name, in_name,format='AAIGrid')"
  )
end



;; save habitat quality of every parcel as patch-variable
to natcap_invest_save_habitatquality_to_patch [experiment]
  print ["saving habitat quality scores as patch variable within EFForTS-ABM"]
  let quality-outputfile-name ""

  set quality-outputfile-name (word workdir "/output/quality_c_" experiment ".asc")

  file-open quality-outputfile-name
  let temp []
  repeat 6 [let header file-read-line] ; skip first 6 lines of header
  while [file-at-end? = FALSE][
         set temp lput file-read temp 
        ]
 file-close
(foreach sort patches temp [
       [a b] -> ask a [ set p_habitat_quality b] 
 ] )
end

to natcap_invest_aggregate_habitatquality_landscape
  print ["aggregating habitat quality scores"]
  set landscape-hq 0
  set landscape-hq mean [p_habitat_quality] of patches
  print (word "landscape-hq mean:" landscape-hq)
end

to natcap_invest_aggregate_habitatquality_landuse
  print ["aggregating habitat quality scores for each landuse"]
  set forest-hq 0
  set forest-hq mean [p_habitat_quality] of patches with [p_landuse = -100]
  print (word "forest-hq mean:" forest-hq)
  if any? patches with [p_landuse = 0]
  [
  set oilpalm-hq 0
  set oilpalm-hq mean [p_habitat_quality] of patches with [p_landuse = 0]
  print (word "oilpalm-hq mean:" oilpalm-hq)
  ]
  if any? patches with [p_landuse = 1]
  [
  set rubber-hq 0
  set rubber-hq mean [p_habitat_quality] of patches with [p_landuse = 1]
  print (word "rubber-hq mean:" rubber-hq)
  ]
end


